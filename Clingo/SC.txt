% Building / Unit types
buildingType(marineType).	
buildingType(probeType).	
buildingType(barracksType).
buildingType(supplyDepotType).	

% Size specifications
width(marineType,1).	height(marineType,1).
width(probeType,2).		height(probeType,1).
width(barracksType,4).	height(barracksType,3).
width(supplyDepotType,3). 	height(supplyDepotType,2).

% Gaps
leftGap(barracksType,16). 	rightGap(barracksType,15).	topGap(barracksType,16). 	bottomGap(barracksType,7).
leftGap(marineType,0). 		rightGap(marineType,0). 	topGap(marineType,0). 		bottomGap(marineType,0).
leftGap(supplyDepotType,12).		 rightGap(supplyDepotType,11). 	topGap(supplyDepotType,8). 		bottomGap(supplyDepotType,11).

% Facts
building(marine1).	type(marine1, marineType).
building(probe1).	type(probe1, probeType).
building(barracks1).	type(barracks1, barracksType).
building(supplyDepot1).	type(supplyDepot1, supplyDepotType).

% Constraint: two units/buildings cannot occupy the same tile
:- occupiedBy(B1, X, Y), occupiedBy(B2, X, Y), B1 != B2.

% Tiles occupied by buildings
occupiedBy(B,X2,Y2) :- place(B, X1, Y1),
						type(B, BT), width(BT,Z), height(BT, Q),
						X2 >= X1, X2 < X1+Z, Y2 >= Y1, Y2 < Y1+Q,
						walkableTile(X2, Y2).
						

% Gaps between two adjacent tiles, occupied by buildings.
verticalGap(X1,Y1,X2,Y2,G) :-
	occupiedBy(B1,X1,Y1), occupiedBy(B2,X2,Y2),
	B1 != B2, X1=X2, Y1=Y2-1, G=S1+S2,
	type(B1,T1), type(B2,T2), bottomGap(T1,S1), topGap(T2,S2).
	
verticalGap(X1,Y1,X2,Y2,G) :-
	occupiedBy(B1,X1,Y1), occupiedBy(B2,X2,Y2),
	B1 != B2, X1=X2, Y1=Y2+1, G=S1+S2,
	type(B1,T1), type(B2,T2), bottomGap(T2,S2), topGap(T1,S1).
	
horizontalGap(X1,Y1,X2,Y2,G) :-
	occupiedBy(B1,X1,Y1), occupiedBy(B2,X2,Y2),
	B1 != B2, X1=X2-1, Y1=Y2, G=S1+S2,
	type(B1,T1), type(B2,T2), rightGap(T1,S1), leftGap(T2,S2).

horizontalGap(X1,Y1,X2,Y2,G) :-
	occupiedBy(B1,X1,Y1), occupiedBy(B2,X2,Y2),
	B1 != B2, X1=X2+1, Y1=Y2, G=S1+S2,
	type(B1,T1), type(B2,T2), rightGap(T2,S2), leftGap(T1,S1).



% Tile information
walkableTile(1,1..4).
walkableTile(2..5,1..8).
walkableTile(6..8,2..8).
walkableTile(9,3..8).
walkableTile(10..11,4..8).

buildable(barracksType, 2..8, 5).
buildable(barracksType, 4..8, 4).
buildable(barracksType, 5..6, 3).

buildable(supplyDepotType, 2..9, 5..6). 
buildable(supplyDepotType, 4..9, 4).
buildable(supplyDepotType, 5..7, 3).
buildable(supplyDepotType, 6, 2).

buildable(marineType, 2..11, 5).
buildable(marineType, 4..11, 4).
buildable(marineType, 5..9, 3).
buildable(marineType, 6..8, 2).

outsideBase(2,2).	insideBase(7,8).



% Constraint: Inside of the base must not be reachable.
:- insideBase(X2,Y2), outsideBase(X1,Y1), canReach(X2,Y2).

% Reachability between tiles.
blocked(X,Y) :- occupiedBy(B,X,Y), building(B), walkableTile(X,Y).
canReach(X,Y) :- outsideBase(X,Y).

canReach(X2,Y) :-
	canReach(X1,Y), X1=X2+1, walkableTile(X1,Y), walkableTile(X2,Y),
	not blocked(X1,Y), not blocked(X2,Y).
canReach(X2,Y) :-
	canReach(X1,Y), X1=X2-1, walkableTile(X1,Y), walkableTile(X2,Y),
	not blocked(X1,Y), not blocked(X2,Y).
canReach(X,Y2) :-
	canReach(X,Y1), Y1=Y2+1, walkableTile(X,Y1), walkableTile(X,Y2),
	not blocked(X,Y1), not blocked(X,Y2).
canReach(X,Y2) :-
	canReach(X,Y1), Y1=Y2-1, walkableTile(X,Y1), walkableTile(X,Y2),
	not blocked(X,Y1), not blocked(X,Y2).
canReach(X2,Y2) :-
	canReach(X1,Y1), X1=X2+1, Y1=Y2+1, walkableTile(X1,Y1), walkableTile(X2,Y2),
	not blocked(X1,Y1), not blocked(X2,Y2).
canReach(X2,Y2) :-
	canReach(X1,Y1), X1=X2-1, Y1=Y2+1, walkableTile(X1,Y1), walkableTile(X2,Y2),
	not blocked(X1,Y1), not blocked(X2,Y2).
canReach(X2,Y2) :-
	canReach(X1,Y1), X1=X2+1, Y1=Y2-1, walkableTile(X1,Y1), walkableTile(X2,Y2),
	not blocked(X1,Y1), not blocked(X2,Y2).
canReach(X2,Y2) :-
	canReach(X1,Y1), X1=X2-1, Y1=Y2-1, walkableTile(X1,Y1), walkableTile(X2,Y2),
	not blocked(X1,Y1), not blocked(X2,Y2).

% Using gaps to reach (walk on) blocked locations.
enemyUnitX(16). enemyUnitY(16).
canReach(X1,Y1) :- horizontalGap(X1,Y1,X2,Y1,G), G >= S, X2=X1+1, canReach(X1,Y3), Y3=Y1+1, enemyUnitX(S).
canReach(X1,Y1) :- horizontalGap(X1,Y1,X2,Y1,G), G >= S, X2=X1-1, canReach(X1,Y3), Y3=Y1+1, enemyUnitX(S).
canReach(X1,Y1) :- horizontalGap(X1,Y1,X2,Y1,G), G >= S, X2=X1+1, canReach(X1,Y3), Y3=Y1-1, enemyUnitX(S).
canReach(X1,Y1) :- horizontalGap(X1,Y1,X2,Y1,G), G >= S, X2=X1-1, canReach(X1,Y3), Y3=Y1-1, enemyUnitX(S).
canReach(X1,Y1) :- verticalGap(X1,Y1,X1,Y2,G), G >= S, Y2=Y1+1, canReach(X3,Y1), X3=X1-1, enemyUnitY(S).
canReach(X1,Y1) :- verticalGap(X1,Y1,X1,Y2,G), G >= S, Y2=Y1-1, canReach(X3,Y1), X3=X1-1, enemyUnitY(S).
canReach(X1,Y1) :- verticalGap(X1,Y1,X1,Y2,G), G >= S, Y2=Y1+1, canReach(X3,Y1), X3=X1+1, enemyUnitY(S).
canReach(X1,Y1) :- verticalGap(X1,Y1,X1,Y2,G), G >= S, Y2=Y1-1, canReach(X3,Y1), X3=X1+1, enemyUnitY(S).	
	
	
% Generate all the potential placements.
1[place(marine1,X,Y) : buildable(marineType,X,Y)]1.
1[place(supplyDepot1,X,Y) : buildable(supplyDepotType,X,Y)]1.
1[place(barracks1,X,Y) : buildable(barracksType,X,Y)]1.


% Optimization criterion
#minimize [verticalGap(X1,Y1,X2,Y2,G) = G ].
#minimize [horizontalGap(X1,Y1,X2,Y2,G) = G ].

#hide.
#show place/3.
%#show walkableTile/2.